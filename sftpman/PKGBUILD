# Maintainer: Slavi Pantaleev <s.pantaleev at gmail.com>

pkgname=sftpman
epoch=1
pkgver=0.6.0
pkgrel=1
pkgdesc="Application that handles sshfs/sftp file systems mounting."
arch=('any')
url="https://github.com/spantaleev/sftpman"
license=('BSD')
depends=('python' 'sshfs' 'python-distribute')
makedepends=('git')
optdepends=('sftpman-gtk: GTK frontend')
install=$pkgname.install

git_location="git://github.com/spantaleev/sftpman.git"
git_branch=$pkgver

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [ -d $pkgname ] ; then
		cd $pkgname
		# Switch back to master as we're now on a tagged branch
		git checkout master
		git pull origin
		msg "The local files are updated."
		git checkout $git_branch
	else
		git clone $git_location $pkgname
		cd $pkgname
		git checkout $git_branch
	fi

	msg "GIT checkout done"
}

package() {
	cd "$srcdir/$pkgname"
	python setup.py install --root=$pkgdir/ --optimize=1

	install -Dm 644 $srcdir/$pkgname/LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
	install -Dm 755 $srcdir/$pkgname/sleep-scripts/sleep.d/49-sftpman-unmount-all $pkgdir/etc/pm/sleep.d/49-$pkgname
	install -Dm 755 $srcdir/$pkgname/sleep-scripts/system-sleep/sftpman-unmount-all.sh $pkgdir/usr/lib/systemd/system-sleep/sftpman-unmount-all.sh
	install -Dm 644 $srcdir/$pkgname/bash_completion.d/$pkgname $pkgdir/etc/bash_completion.d/$pkgname
}
